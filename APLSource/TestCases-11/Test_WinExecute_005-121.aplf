 R←Test_WinExecute_005(stopFlag batchFlag);⎕TRAP;rc;sec;successFlag;debugging;parms;result;filename;more;buff;EndIf
⍝ Load a WS and write the result of `WinSys.IsRunningAsAdmin` to a file
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

 :If 'Win'≢#.APLTreeUtils.GetOperatingSystem ⍬
     R←∆WindowsOnly
     :Return
 :EndIf

 parms←##.OS.CreateParms_WinExecute
 parms.file←'"',(↑#.GetCommandLineArgs),'"'
 parms.lpParms,←'"',#.FilesAndDirs.PWD,'/Tests/Test_WinExecute_005"'
 parms.lpParms,←' session_file=""'
 parms.lpParms,←' maxws=8MB'
⍝parms.lpParms,←' -x'  ⍝ For debugging the loaded WS
 filename←#.FilesAndDirs.PWD,'/Tests/Test_WinExecute_005.txt'
 #.FilesAndDirs.DeleteFile filename
 (successFlag rc more)←##.OS.WinExecute parms
 →PassesIf 1=successFlag
 →PassesIf 42=rc
 →PassesIf 0∊⍴more
 sec←filename{⎕NEXISTS ⍺:⍵ ⋄ _←⎕DL 1 ⋄ 10=⍵:¯1 ⋄ ⍺ ∇ ⍵+1}0
 →GoToTidyUp ¯1=sec ⍝ Something went wrong!
 buff←#.APLTreeUtils.ReadUtf8File filename
 →GoToTidyUp 1≠↑⎕VFI buff
 →GoToTidyUp 0≠1⊃⎕VFI buff
 #.FilesAndDirs.DeleteFile filename

⍝ Now again but with admin rights - this will fail if you do not have admin rights!

 (successFlag rc more)←1 ##.OS.WinExecute parms
 →PassesIf 1=successFlag
 →PassesIf 42=rc
 →PassesIf 0∊⍴more
 sec←filename{⎕NEXISTS ⍺:⍵ ⋄ _←⎕DL 1 ⋄ 10=⍵:¯1 ⋄ ⍺ ∇ ⍵+1}0
 →GoToTidyUp ¯1=sec ⍝ Something went wrong!
 buff←#.APLTreeUtils.ReadUtf8File filename
 →GoToTidyUp 1≠↑⎕VFI buff
 →GoToTidyUp 1≠1⊃⎕VFI buff

 R←∆OK

∆TidyUp:
 1 ⎕NDELETE filename
