R←Test_ShellExecute_006(stopFlag batchFlag);parms;MyAPL;tempPath;filename;list;⎕TRAP
⍝ Start APL in its own process and then kill it, testing `KillPID` in the process
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 parms←APLProcess.CreateParms
 parms.Wait←0
 parms.Runtime←1
 parms.WorkspaceName←##.CiderConfig.HOME,'/Tests/Test_ToBeKilled.dws'

 tempPath←FilesAndDirs.GetTempPath
 filename←tempPath,'Test-ToBeKilled.*'
 FilesAndDirs.DeleteFile filename            ⍝ Get rid of any left-overs; this will be re-created by the process started

 MyAPL←⎕NEW APLProcess(,⊂parms)
 Assert MyAPL.HasExited=0
 ⎕dl 1

 list←##.OS.GetTaskList 0 0 0
 →T.PassesIf MyAPL.GetPID∊list[;2]

 →T.PassesIf 1=##.OS.KillPID MyAPL.GetPID
 →T.PassesIf 1=MyAPL.HasExited
 list←##.OS.GetTaskList 0 0 0
 →T.PassesIf ~MyAPL.GetPID∊list[;2]

 R←T._OK
