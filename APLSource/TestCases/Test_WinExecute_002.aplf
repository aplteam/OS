 R←Test_WinExecute_002(stopFlag batchFlag);⎕TRAP;rc;sec;filename;cmd;successFlag;debugging;more
⍝ Starts another instance of Dyalog which loads a WS that creates a file; parameters a passed as text string
⍝ The started WS waits for 3 seconds and then creates a file "Test_Win_002" as a sibling of the WS
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 :If 'Win'≢APLTreeUtils2.GetOperatingSystem ⍬
     R←T._WindowsOnly
     :Return
 :EndIf

 cmd←'"',(⊃#.GetCommandLineArgs),'"'
 cmd,←' "',##.CiderConfig.HOME,'/Tests/Test_WinExecute"'
 cmd,←' session_file=""'
 cmd,←' maxws=16MB'
 :If 0
     cmd,←' -x'For debugging the loaded WS
 :EndIf
 filename←##.CiderConfig.HOME,'/Tests/Test_WinExecute.txt'
 FilesAndDirs.DeleteFile filename
 (successFlag rc more)←##.OS.WinExecute cmd
 →T.GoToTidyUp 1≠successFlag
 →T.GoToTidyUp 42≠rc
 →T.GoToTidyUp 0≠≢more
 sec←filename{FilesAndDirs.Exists ⍺:⍵ ⋄ _←⎕DL 1 ⋄ 10=⍵:¯1 ⋄ ⍺ ∇ ⍵+1}0
 →T.GoToTidyUp ¯1=sec ⍝ Something went wrong!
 →T.GoToTidyUp 3>sec

 R←T._OK

∆TidyUp:
 FilesAndDirs.DeleteFile filename
